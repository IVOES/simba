#
# @file make/platformio.sconscript
# @version 1.0
#
# @section License
# Copyright (C) 2014-2015, Erik Moqvist
#
# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.
#
# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# This file is part of the Simba project.
#

import os

# Map the PlatformIO board name to the Simba board name
BOARD_MAP = {
    "due": "arduino_due",
    "megaatmega2560": "arduino_mega",
    "nanoatmega328": "arduino_nano"
}

env = DefaultEnvironment()

# default values
if "SIMBA_ROOT" not in env:
    env.Append(SIMBA_ROOT=os.path.join(Dir(".").abspath, ".."))

# Rename the name of boards that have different name in PlatformIO and
# Simba.
if env["BOARD"] in BOARD_MAP:
    env["BOARD"] = BOARD_MAP[env["BOARD"]]

# Remove defines that breaks the build.
# -F_CPU -> duplicate define
cppdefines = []
for define in env["CPPDEFINES"]:
    if "F_CPU=" not in define:
        cppdefines.append(define)
env.Replace(CPPDEFINES=cppdefines)

# Remove flags that breaks the build.
# -Os -> internal compiler error
cppflags = []
for flag in env["CPPFLAGS"]:
    if (("-Os" not in flag) and
        ("-g" not in flag)):
        cppflags.append(flag)
env.Replace(CPPFLAGS=cppflags)

env.Replace(LINKFLAGS=[])

SConscript("app_common.sconscript")

# Add the simba library to the list of libraries that will be built
env.Append(LIBS=[env.Library("#/simba", env["OBJS"])])
